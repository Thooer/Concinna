function(Collect out_var dir)
  if(NOT EXISTS "${dir}")
    set(${out_var} "" PARENT_SCOPE)
    return()
  endif()
  file(GLOB_RECURSE _mod_files CONFIGURE_DEPENDS
    "${dir}/*.ixx"
    "${dir}/*.cppm"
  )
  set(${out_var} ${_mod_files} PARENT_SCOPE)
endfunction()

function(API build_name api_dir)
  cmake_parse_arguments(ARGS "" "" "LINK" ${ARGN})
  string(REPLACE "." ";" _parts "${build_name}")
  list(LENGTH _parts _len)
  math(EXPR _last_idx "${_len}-1")
  list(GET _parts ${_last_idx} _target)
  set_property(GLOBAL PROPERTY "MODULE_TARGET_TO_BUILDNAME_${_target}" "${build_name}")
  Collect(_api_files ${api_dir})
  file(GLOB_RECURSE _hdr_files CONFIGURE_DEPENDS "${api_dir}/*.h" "${api_dir}/*.hpp")
  set_property(GLOBAL PROPERTY "MODULE_${build_name}_API_DIR" "${api_dir}")
  set_property(GLOBAL PROPERTY "MODULE_${build_name}_API_FILES" "${_api_files}")
  set_property(GLOBAL PROPERTY "MODULE_${build_name}_API_HDR_FILES" "${_hdr_files}")
  if(ARGS_LINK)
    set_property(GLOBAL PROPERTY "MODULE_${build_name}_API_LINK" "${ARGS_LINK}")
  endif()
endfunction()

function(Impl build_or_impl_name impl_root)
  cmake_parse_arguments(ARGS "" "" "EXCLUDE_FILES;LINK" ${ARGN})
  string(REPLACE "." ";" _parts "${build_or_impl_name}")
  list(LENGTH _parts _len)
  if(_len LESS 2)
    message(FATAL_ERROR "Impl requires build.and.backend name (e.g., Primitive.System.Windows)")
  endif()
  math(EXPR _last_idx "${_len}-1")
  list(GET _parts ${_last_idx} _backend)
  list(REMOVE_AT _parts ${_last_idx})
  string(JOIN "." _build_name ${_parts})
  set_property(GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_ROOT" "${impl_root}")
  Collect(_impl_sel_files "${impl_root}/${_backend}")
  if(ARGS_EXCLUDE_FILES)
    list(REMOVE_ITEM _impl_sel_files ${ARGS_EXCLUDE_FILES})
  endif()
  get_property(_impl_names GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_NAMES")
  if(NOT _impl_names)
    set(_impl_names "")
  endif()
  list(APPEND _impl_names "${_backend}")
  list(REMOVE_DUPLICATES _impl_names)
  set_property(GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_NAMES" "${_impl_names}")
  set_property(GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_${_backend}_FILES" "${_impl_sel_files}")
  if(ARGS_LINK)
    set_property(GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_${_backend}_LINK" "${ARGS_LINK}")
  endif()
endfunction()

function(Build target)
  cmake_parse_arguments(ARGS "" "TYPE;BACKEND" "LINK" ${ARGN})
  if(NOT ARGS_TYPE)
    set(ARGS_TYPE STATIC)
  endif()
  get_property(_build_name GLOBAL PROPERTY "MODULE_TARGET_TO_BUILDNAME_${target}")
  if(NOT _build_name)
    message(FATAL_ERROR "Build: target ${target} has no associated build name; call API with build name first")
  endif()
  get_property(_api_dir GLOBAL PROPERTY "MODULE_${_build_name}_API_DIR")
  get_property(_api_files GLOBAL PROPERTY "MODULE_${_build_name}_API_FILES")
  get_property(_hdr_files GLOBAL PROPERTY "MODULE_${_build_name}_API_HDR_FILES")
  get_property(_api_link GLOBAL PROPERTY "MODULE_${_build_name}_API_LINK")
  if(NOT _api_dir)
    message(FATAL_ERROR "API not defined for ${_build_name}")
  endif()
  if(ARGS_TYPE STREQUAL SHARED)
    add_library(${target} SHARED)
  else()
    add_library(${target} STATIC)
  endif()
  if(_api_files)
    target_sources(${target}
      PUBLIC
        FILE_SET CXX_MODULES TYPE CXX_MODULES
          BASE_DIRS ${_api_dir}
        FILES ${_api_files}
    )
  endif()
  if(_hdr_files)
    target_sources(${target}
      PUBLIC
        FILE_SET HEADERS TYPE HEADERS
          BASE_DIRS ${_api_dir}
        FILES ${_hdr_files}
    )
    target_include_directories(${target} PUBLIC ${_api_dir})
  endif()
  get_property(_impl_names GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_NAMES")
  if(NOT ARGS_BACKEND)
    if(_impl_names)
      message(FATAL_ERROR "Build(${target}): BACKEND must be provided as full name, e.g. Primitive.Platform.Windows")
    else()
      set(_sel "")
    endif()
  endif()
  if(ARGS_BACKEND)
    string(REPLACE "." ";" _sel_parts "${ARGS_BACKEND}")
    list(LENGTH _sel_parts _sel_len)
    if(_sel_len LESS 2)
      message(FATAL_ERROR "Build(${target}): BACKEND must be a full name of build+backend, e.g. Primitive.Platform.Windows")
    endif()
    math(EXPR _sel_last_idx "${_sel_len}-1")
    list(GET _sel_parts ${_sel_last_idx} _sel)
    list(REMOVE_AT _sel_parts ${_sel_last_idx})
    string(JOIN "." _sel_build_name ${_sel_parts})
    if(NOT _sel_build_name STREQUAL "${_build_name}")
      message(FATAL_ERROR "Build(${target}): BACKEND build name '${_sel_build_name}' does not match API build name '${_build_name}'")
    endif()
  endif()
  if(_sel)
    set_property(GLOBAL PROPERTY "MODULE_${_build_name}_SELECTED_BACKEND" "${_sel}")
  endif()
  get_property(_impl_names GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_NAMES")
  if(_sel AND _impl_names)
    list(FIND _impl_names "${_sel}" _idx)
    if(NOT _idx EQUAL -1)
      get_property(_impl_files GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_${_sel}_FILES")
      if(_impl_files)
        target_sources(${target} PRIVATE ${_impl_files})
      endif()
      get_property(_impl_link GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_${_sel}_LINK")
      if(_impl_link)
        target_link_libraries(${target} PRIVATE ${_impl_link})
      endif()
    endif()
  endif()
  get_property(_impl_root GLOBAL PROPERTY "MODULE_${_build_name}_IMPL_ROOT")
  if(_impl_root)
    file(GLOB _impl_root_files CONFIGURE_DEPENDS "${_impl_root}/*.ixx" "${_impl_root}/*.cppm")
    if(_impl_root_files)
      target_sources(${target} PRIVATE ${_impl_root_files})
    endif()
  endif()
  if(_api_link)
    target_link_libraries(${target} PUBLIC ${_api_link})
  endif()
  if(ARGS_LINK)
    target_link_libraries(${target} PUBLIC ${ARGS_LINK})
  endif()
  set_target_properties(${target} PROPERTIES LINKER_Prim CXX)
  apply_common_options(${target})
endfunction()

function(Sample name sample_dir)
  cmake_parse_arguments(ARGS "" "OPTION" "LINK" ${ARGN})
  set(_exe "${name}")
  set(_opt "${ARGS_OPTION}")
  if(NOT _opt)
    string(REPLACE "." "_" _opt_default ${_exe})
    string(TOUPPER ${_opt_default} _opt_upper)
    set(_opt "BUILD_${_opt_upper}")
  endif()
  option(${_opt} "Build sample" OFF)
  if(NOT ${_opt})
    return()
  endif()
  Collect(_mod_files ${sample_dir})
  file(GLOB_RECURSE _cpp_files CONFIGURE_DEPENDS "${sample_dir}/*.cpp")
  list(APPEND _mod_files ${_cpp_files})
  if(_mod_files)
    add_executable(${_exe} ${_mod_files})
    if(ARGS_LINK)
      target_link_libraries(${_exe} PRIVATE ${ARGS_LINK})
    endif()
    apply_common_options(${_exe})
  endif()
endfunction()
